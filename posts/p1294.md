---
title: ソースコードジェネレータ | 現場至上主義 Spring Boot2 徹底活用
date: 2022.07.29
description: ソースコードジェネレータ | 現場至上主義 Spring Boot2 徹底活用
categories: ['Spring']
---

正直、私がこの本で一番興味があったのがソースコードジェネレータの箇所です。

ソースコードの自動生成はやったことがあります。画面もそうですが、DB定義からdaoとかを自動生成したりするものもあります（S2JDBC-Gen）

本の内容としては実装詳細は省かれていたので、こちらも調べつつ実装してみます。基本的にはテンプレートとなるtxtファイルを用意しておいて、そこに値を埋め込んでソースコードを作成するようです。

どうやって値を埋め込むかというとThymeleafを利用します。

ソースとしてはここら辺が該当します

https://github.com/miyabayt/spring-boot-doma2-sample/tree/master/buildSrc/src/main


参考サイト

http://gradle.monochromeroad.com/docs/userguide/custom_tasks.html


https://qiita.com/niwasawa/items/5f0f5c08fbcedd2221a7


この記事をブラッシュアップした記事を投稿しました

[リンク](/posts/p3149)


## Gradle


gradleについて詳しくないので調べつつやっていきます

とりあえずGthubソースのgradleバージョンが低いため、compileはimplementationに置き換える必要がある。

またプロジェクトルートのgradleはcom.ドメイン名を省略しています。
```
apply plugin: CodeGenPlugin
```

フォルダ構成は参考のGithubに合わせて作成します

![画像](/1294/1.png)


build.gradleの内容は記事の終わりに記載しています。



## 空タスクの作成


とりあえず自作のタスクを作りますが、そこのひな型は完全に書籍を参考にしています。

```ts
package com.volkruss

import org.gradle.api.Plugin
import org.gradle.api.Project

class CodeGenPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {
        project.extensions.create("codegen", CodeGenPluginExtension.class)
        project.task("codegen", type: GenerateTask)
    }
}

```


```ts
package com.volkruss

import org.gradle.api.DefaultTask
import org.gradle.api.tasks.TaskAction

class GenerateTask extends DefaultTask {
    @TaskAction
    def codegen(){
        println("called!")
    }
}
```


```ts
package com.volkruss

class CodeGenPluginExtension {

}
```


認識されれば以下のようにタスクとして追加されます

![画像](/1294/2.png)


実行するとcalled!と出力されます
```
> Task :codegen
called!

```

## 引数とエクステンションの読み取り


引数を受取ってみます。引数を渡すには -P引数名としてあげれば良さそうです。受取るにはprojectオブジェクトを利用します。

簡単な例で引数の動きを確認します。

```ts
class GenerateTask extends DefaultTask {
    @TaskAction
    def codegen(){
        println("called!")
        if (!project.hasProperty("misaka")) {
            print("misakaが設定されていません")
            return
        }
        print(project.misaka.toString())
        print("-----end-----")
}
```

```
PS D:\work\misaka\misaka> gradle codegen -Pmisaka=mikoto
Starting a Gradle Daemon, 1 busy and 2 incompatible Daemons could not be reused, use --status for details

> Task :codegen
called!
mikoto-----end-----


PS D:\work\misaka\misaka> gradle codegen -Pkuroko=sirai

> Task :codegen
called!
misakaが設定されていません


```

次にエクステンションから値を取得します。

エクステンションから値を受取るには以下のようにします。
```
project.extensions.findByType(Class)
```

簡単な例でエクステンションから値を受取ってみます。

まずはエクステンションにnameプロパティを追加します。

```ts
class CodeGenPluginExtension {
    String name = "Misaka Mikoto"
}
```


次にタスクからエクステンションの値を取得します。

```ts
    @TaskAction
    def codegen(){
        println("called!")
        if (!project.hasProperty("misaka")) {
            print("misakaが設定されていません")
            return
        }
        print(project.misaka.toString())
        print("-----end-----")

        //エクステンションから値を読み込む
        def ext = project.extensions.findByType(CodeGenPluginExtension)
        print(ext.name)
    }
```

```
PS D:\work\misaka\misaka> gradle codegen -Pmisaka=mikoto

> Task :codegen
called!
mikoto-----end-----Misaka Mikoto

```

ここまでの知識があればサンプルのソースコードは理解できるようになると思います。

## テンプレートの作成


次にtxtファイルからテンプレートとなるひな形を取得して、Thymeleafを利用して値を埋め込みます。今回は参考GithubからDto.txtを利用します。

![画像](/1294/3.png)


少しだけtxtファイルは修正しました。※パッケージ箇所commonDtoPackageNameは利用しないため除去します。そのため継承も削除します。

```java
package [[${domainBaseName}]].[[${dtoPackageName}]].[[${subSystem}]];

import org.seasar.doma.*;

import lombok.Getter;
import lombok.Setter;

@Table(name = "[[${lowerFuncName}]]s") //現状ではyでもysになる
@Entity
@Getter
@Setter
public class [[${upperFuncName}]] {

    private static final long serialVersionUID = -1L;

    @Id
    @Column(name = "[[${lowerFuncName}]]_id")
    @GeneratedValue(strategy = GenerationType.IDENTITY)
    Long id;

    String [[${lowerFuncName}]]Name;
}
```


エクステンションファイルは以下のように修正します。いらない設定もありますが拡張性を考慮して残しておきます。

```java
class CodeGenPluginExtension {
    String srcDirName = "src/main/java/"
    String testDirName = "src/test/groovy/"
    String sqlDirName = "src/main/resources/META-INF/"
    String htmlDirName = "src/main/resources/templates/modules/"
    String domainProjectName = "D:\\work\\misaka\\misaka"
    String webProjectName

    String daoPackageName
    String dtoPackageName = "domain.dto"
    String commonDtoPackageName
    String repositoryPackageName
    String servicePackageName
    String commonServicePackageName
    String exceptionPackageName
    String webBasePackageName
    String baseControllerPackageName
    String controllerPackageName
    String baseValidatorPackageName
    //　追加
    String domainBaseName = "com.volkruss.misaka"
}
```


タスクの実装は参考Githubを元に以下のように作成します

```java
class GenerateTask extends DefaultTask {
    @TaskAction
    def codegen(){

        // 利用例
        // gradle codegen -Ptarget=dto -PsubSystem=expenses -Pfunc=salary

        println("----start-----")

        // 必須項目チェック / subSystem
        if (!project.hasProperty("subSystem")) {
            println("subSystem must not be null")
            return
        }
        // 必須項目チェック / func
        if (!project.hasProperty("func")) {
            println("func must not be null")
            return
        }

        // 今回はdtoのみだが、今後作るものによっては判別できる
        def target = null
        if(project.hasProperty("target")) {
            target = project.target;
        }

        //　エクステンションの取得
        def ext = project.extensions.findByType(CodeGenPluginExtension)
        // 利用変数定義
        def subSystem = StringUtils.lowerCase(project.subSystem.toString())
        def func = StringUtils.capitalize(project.func.toString())
        def lowerFunc = StringUtils.lowerCase(func)
        // Thymeleafに渡す変数を格納したMap
        def objects = extensionsToMap()
        // 機能名
        objects.put("subSystem", subSystem)
        // クラス名
        objects.put("upperFuncName", func)
        // テーブル及び、カラムのプレフィックス名
        objects.put("lowerFuncName", lowerFunc)

        //　出力先のフォルダパス
        def domainTarget = "${ext.domainProjectName}/${ext.srcDirName}/${ext.domainBaseName}"

        // 今回はdtoを受けたらない場合は作成しない
        // if (target == null || target.equals("dto")) {
        if (target.equals("dto")) {
            // テキストファイル
            // dtoファイルの出力パス
            // Mapオブジェクト
            generate("templates/src/Dto.txt", getTargetPath(domainTarget, "/${ext.dtoPackageName}/${subSystem}/${func}", ".java"), objects)
        }

    }

    // 出力先のディレクトリ（エクステンション）
    // ファイル名（引数）
    // 拡張子（固定値）

    // 完全なパスを返します。
    // パスの想定 domain/dto/{sales}
    Path getTargetPath(String _target, String _fileName, String suffix) {
        def target = StringUtils.replace(_target, ".", "/")
        def sb = new StringBuilder().append(target)
        def fileName = StringUtils.replace(_fileName, ".", "/")
        return Paths.get(sb.toString(), "${fileName}${suffix}")
    }

    //　Thymeleafにて取得したStringの内容を、設定したパスにUTF-8で書き込む
    void generate(String template, Path path, Map<String, Object> objects) {
        def body = processTemplate(template, objects)
        def f = path.toFile()
        f.parentFile.mkdirs()
        f.createNewFile()
        f.write(body, "UTF-8")
    }

    String processTemplate(String template, Map<String, Object> objects) {
        def resolver = new ClassLoaderTemplateResolver()
        resolver.setTemplateMode("TEXT")
        resolver.setCharacterEncoding("UTF-8")

        def templateEngine = new TemplateEngine()
        templateEngine.setTemplateResolver(resolver)

        def context = new Context()
        // 引数で受取った値を元に設定したMapから変数を設定する
        if (objects != null && !objects.isEmpty()) {
            objects.each { key, value ->
                context.setVariable(key, value)
            }
        }
        // 指定したテンプレート（テキストデータ）に対してcontextの内容を埋め込んでStringを返す（Modelのような感じ）
        return templateEngine.process(template, context)
    }

    Map<String, Object> extensionsToMap() {

        def ext = project.extensions.findByType(CodeGenPluginExtension)

        Map<String, Object> objects = new HashMap<>()
        objects.put("commonDtoPackageName", ext.commonDtoPackageName)
        objects.put("dtoPackageName", ext.dtoPackageName)
        objects.put("daoPackageName", ext.daoPackageName)
        objects.put("repositoryPackageName", ext.repositoryPackageName)
        objects.put("servicePackageName", ext.servicePackageName)
        objects.put("commonServicePackageName", ext.commonServicePackageName)
        objects.put("exceptionPackageName", ext.exceptionPackageName)
        objects.put("webBasePackageName", ext.webBasePackageName)
        objects.put("baseControllerPackageName", ext.baseControllerPackageName)
        objects.put("controllerPackageName", ext.controllerPackageName)
        objects.put("baseValidatorPackageName", ext.baseValidatorPackageName)
        objects.put("domainBaseName", ext.domainBaseName)

        return objects
    }
}
```


以下の用に引数を設定して実行します
```
gradle codegen -Ptarget=dto -PsubSystem=expenses -Pfunc=salary

```

成功すると対象フォルダにSalary.javaが作成されています。

![画像](/1294/4.png)


出力されたファイルは問題なさそうです。※yをiesにすることは別途対応できそうです。※依存を入れてないパッケージがあるのでエラーになっていますが、依存関係を解決すればエラーも消えるでしょう

![画像](/1294/5.png)




ソースコードジェネレータと聞いて、何か特別なプラグインとかがあるのかと最初は思ったのですが、Thymeleafを利用しますが割と原始的な方法ですね。

これで面倒なファイル作成、およびパッケージ構成のルール定義が解決できます。

## build.gradle


最後になりますがbuild.gradleを掲載しておきます。

buildSrc/build.gradle

```ts
buildscript {
    ext {
        springBootVersion = "2.0.6.RELEASE"
        groovyVersion = "2.5.3"
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'groovy'
apply plugin: "io.spring.dependency-management"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}
dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}
dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "org.apache.commons:commons-lang3"
    implementation "org.thymeleaf:thymeleaf"
}
```


プロジェクトルート/build.gradle

```ts
import com.volkruss.CodeGenPlugin

buildscript {
	ext {
		dockerComposePluginVersion = "0.15.0"
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath "com.avast.gradle:gradle-docker-compose-plugin:${dockerComposePluginVersion}"
	}
}

plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.volkruss'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

apply plugin: "docker-compose"
apply plugin: CodeGenPlugin // ここを追加


dockerCompose {
	useComposeFiles = ["docker/docker-compose.yml"]
}

tasks.named('test') {
	useJUnitPlatform()
}

public class GreetingPlugin implements Plugin<Project> {
	@Override
	public void apply(Project project) {
		project.task("hello")
				.doLast(task -> System.out.println("Hello Gradle!"));
	}
}
apply plugin: GreetingPlugin
```

